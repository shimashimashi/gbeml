include(FetchContent)

FetchContent_Declare(googletest
  QUIET
  URL https://github.com/google/googletest/archive/release-1.11.0.tar.gz
)
FetchContent_MakeAvailable(googletest)

FetchContent_Declare(glog
  QUIET
  URL https://github.com/google/glog/archive/v0.5.0.tar.gz
)
FetchContent_MakeAvailable(glog)

add_library(
    gbeml_core SHARED
    core/gameboy.cc
    core/types/types.cc
    core/bus/bus.cc
    core/bus/bus_impl.cc
    core/interrupt/interrupt_controller.cc
    core/interrupt/interrupt_controller_impl.cc
    core/register/register.cc
    core/memory/mbc.cc
    core/memory/ram.cc
    core/memory/ram_impl.cc
    core/memory/rom.cc
    core/cpu/alu.cc
    core/cpu/cpu.cc
    core/cpu/opcode.cc
    core/graphics/fetcher.cc
    core/graphics/lcdc.cc
    core/graphics/lcd_stat.cc
    core/graphics/palette.cc
    core/graphics/pixel.cc
    core/graphics/ppu.cc
    core/graphics/ppu_impl.cc
    core/graphics/sprite.cc
    core/graphics/tile.cc
    core/display/display.cc
    core/timer/timer.cc
    core/timer/timer_impl.cc
    core/joypad/joypad.cc
    core/joypad/joypad_impl.cc
)
if("${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang")
    target_compile_options(
        gbeml_core PRIVATE
        # -Werror -Weverything -Wno-c++98-compat -Wno-c++98-compat-pedantic -Wno-padded
        -Werror -Wall -Wextra
        $<$<CONFIG:DEBUG>:-fsanitize=address,undefined>
        $<$<CONFIG:DEBUG>:--coverage>
    )
    target_link_options(
        gbeml_core PRIVATE
        $<$<CONFIG:DEBUG>:-fsanitize=address,undefined>
        $<$<CONFIG:DEBUG>:--coverage>
    )
else()
    target_compile_options(
        gbeml_core PRIVATE
        -Werror -Wall -Wextra -Wno-return-type
        $<$<CONFIG:DEBUG>:--coverage>
    )
    target_link_options(
        gbeml_core PRIVATE
        $<$<CONFIG:DEBUG>:--coverage>
    )
endif()
target_link_libraries(
    gbeml_core
    glog
)
target_include_directories(gbeml_core PUBLIC . ${CMAKE_SOURCE_DIR}/third_party/debugbreak)

add_executable(
    gbeml
    main.cc
    driver/minifb/minifb_display.cc
    driver/minifb/minifb_window.cc
    driver/stub/stub_display.cc
)
target_link_libraries(gbeml gbeml_core minifb)
target_compile_options(gbeml PRIVATE)

add_executable(
    gbeml_test
    core/bus/bus_impl_test.cc
    core/types/types_test.cc
    core/register/register_test.cc
    core/cpu/alu_test.cc
    core/cpu/cpu_test.cc
    core/graphics/lcdc_test.cc
    core/graphics/lcd_stat_test.cc
    core/graphics/palette_test.cc
    core/graphics/ppu_impl_test.cc
    core/graphics/sprite_test.cc
    core/graphics/tile_test.cc
    core/joypad/joypad_impl_test.cc
    core/timer/timer_impl_test.cc
    core/interrupt/interrupt_controller_impl_test.cc
)
target_link_libraries(
    gbeml_test
    gbeml_core
    gtest_main
    gmock_main
)
target_compile_options(
    gbeml_test PRIVATE
)
target_link_options(
    gbeml_test PRIVATE
)
add_dependencies(gbeml_test clean_gcda)

include(GoogleTest)
gtest_discover_tests(
    gbeml_test
    PROPERTIES LABELS gbeml
)

add_custom_target(
    clean_gcda
    WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
    COMMAND find . -name "*.gcda" -depth -delete
)

add_custom_target(
  coverage
  DEPENDS gbeml_test
  WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
  COMMAND lcov --rc lcov_branch_coverage=1 -c -i -d src/CMakeFiles -o coverage_base.info > lcov.log 2>&1
  COMMAND ctest -L gbeml -j 8
  COMMAND lcov --rc lcov_branch_coverage=1 -d src/CMakeFiles -c -o coverage_test.info > lcov.log 2>&1
  COMMAND lcov --rc lcov_branch_coverage=1 -a coverage_base.info -a coverage_test.info -o coverage_all.info > lcov.log 2>&1
  COMMAND lcov --rc lcov_branch_coverage=1 -r coverage_all.info *_test.cc *gtest* */usr/include/c++/v1/* *glog* *debugbreak* -o coverage.info >> lcov.log 2>&1
  COMMAND genhtml -o lcovHtml --num-spaces 4 -s --legend --branch-coverage coverage.info >> lcov.log 2>&1
  COMMENT [[create coverage report as lcovHtml/index.html, and log as lcov.log]]
)
